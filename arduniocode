#if defined(ESP32)
#include <WiFiMulti.h>
WiFiMulti wifiMulti;
#define DEVICE "ESP32"
#define IR_SENSOR_ENTRY_PIN 13 // Entry IR sensor connected to GPIO 13
#define IR_SENSOR_EXIT_PIN 14  // Exit IR sensor connected to GPIO 14
#elif defined(ESP8266)
#include <ESP8266WiFiMulti.h>
ESP8266WiFiMulti wifiMulti;
#define DEVICE "ESP8266"
#define IR_SENSOR_ENTRY_PIN 2  // Entry IR sensor connected to GPIO 2 for ESP8266
#define IR_SENSOR_EXIT_PIN 3   // Exit IR sensor connected to GPIO 3 for ESP8266
#endif

#include <InfluxDbClient.h>
#include <InfluxDbCloud.h>

// WiFi AP SSID
#define WIFI_SSID "Yesudas"
// WiFi password
#define WIFI_PASSWORD "heteam123"

#define INFLUXDB_URL "https://us-east-1-1.aws.cloud2.influxdata.com"
#define INFLUXDB_TOKEN "NFdWzEBxBFZzurEU4eBUx2J46YKDD6Wj3RCvY9M0nX4MpdV1fI_Xog6IYUdV0Qtru3pqW8KyejVrqCe0gmQaGA=="
#define INFLUXDB_ORG "Amrita"
#define INFLUXDB_BUCKET "gg"

// Time zone info
#define TZ_INFO "UTC5.5"

// Declare InfluxDB client instance with preconfigured InfluxCloud certificate
InfluxDBClient client(INFLUXDB_URL, INFLUXDB_ORG, INFLUXDB_BUCKET, INFLUXDB_TOKEN, InfluxDbCloud2CACert);

// Declare Data point
Point sensor("attendance_records");

// Variables for cooldown timing
unsigned long lastEntryDetectionTime = 0;
unsigned long lastExitDetectionTime = 0;
#define DETECTION_COOLDOWN 5000 // 5 seconds between readings

void setup() {
  Serial.begin(115200);

  // Setup wifi
  WiFi.mode(WIFI_STA);
  wifiMulti.addAP(WIFI_SSID, WIFI_PASSWORD);

  Serial.print("Connecting to wifi");
  while (wifiMulti.run() != WL_CONNECTED) {
    Serial.print(".");
    delay(100);
  }
  Serial.println();

  // Accurate time is necessary for certificate validation and writing in batches
  timeSync(TZ_INFO, "pool.ntp.org", "time.nis.gov");

  // Check server connection
  if (client.validateConnection()) {
    Serial.print("Connected to InfluxDB: ");
    Serial.println(client.getServerUrl());
  } else {
    Serial.print("InfluxDB connection failed: ");
    Serial.println(client.getLastErrorMessage());
  }

  // Configure IR sensor pins
  pinMode(IR_SENSOR_ENTRY_PIN, INPUT);
  pinMode(IR_SENSOR_EXIT_PIN, INPUT);
}

void loop() {
  // Check WiFi connection and reconnect if needed
  if (wifiMulti.run() != WL_CONNECTED) {
    Serial.println("WiFi connection lost");
    return;
  }

  // Read IR sensor values
  int entryIrValue = digitalRead(IR_SENSOR_ENTRY_PIN);
  int exitIrValue = digitalRead(IR_SENSOR_EXIT_PIN);
  unsigned long currentMillis = millis();

  // Check entry sensor with cooldown
  if (entryIrValue == LOW && (currentMillis - lastEntryDetectionTime) > DETECTION_COOLDOWN) {
    sensor.clearFields();
    time_t now;
    time(&now);
    struct tm* localTime = localtime(&now);

    // Log entry event
    sensor.addField("status", "entry_detected");
    sensor.addField("hour", localTime->tm_hour);
    sensor.addField("minute", localTime->tm_min);

    if (!client.writePoint(sensor)) {
      Serial.print("InfluxDB write failed: ");
      Serial.println(client.getLastErrorMessage());
    } else {
      Serial.println("Entry detected and recorded successfully");
    }

    lastEntryDetectionTime = currentMillis;
  }

  // Check exit sensor with cooldown
  if (exitIrValue == LOW && (currentMillis - lastExitDetectionTime) > DETECTION_COOLDOWN) {
    sensor.clearFields();
    time_t now;
    time(&now);
    struct tm* localTime = localtime(&now);

    // Log exit event
    sensor.addField("status", "exit_detected");
    sensor.addField("hour", localTime->tm_hour);
    sensor.addField("minute", localTime->tm_min);

    if (!client.writePoint(sensor)) {
      Serial.print("InfluxDB write failed: ");
      Serial.println(client.getLastErrorMessage());
    } else {
      Serial.println("Exit detected and recorded successfully");
    }

    lastExitDetectionTime = currentMillis;
  }

  // Check buffer size and write if it's getting full
  if (client.isBufferFull()) {
    Serial.println("Buffer full, forcing write");
    client.flushBuffer();
  }

  delay(100);
}
